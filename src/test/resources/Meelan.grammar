{{{package test;

import meelan.*
}}}

grammar Meelan {
    regex ws: [\x09\x0b\r \xa0\u1680\u2000-\u200a\u202f\u205f\u3000]+ ;
	regex nl: "\r\n" | "\n" | "\x0c" | "\x85" | "\u2028" | "\u2029" ; // keep newlines separated for formatting.
    regex slComment: ('/*' .* '*/')! ;
    regex mlComment: '//' ([^\n] | [^\r])* ;

{{{    init {
        tokenizer.addSkipped(ws.tokenId)
        tokenizer.addSkipped(nl.tokenId)
        tokenizer.addSkipped(slComment.tokenId)
        tokenizer.addSkipped(mlComment.tokenId)
    }
}}}

    fragment intRex: [0-9]{1,8} ;
    fragment hexRex: [0-9A-Fa-f]{1,8} ;
    fragment decimals: '.'[0-9]* ;
    fragment exponent: [eE]'-'?[0-9]+ ;
    fragment realRex: [0-9]+(decimals | exponent | decimals exponent) ;
    fragment identifierRex: [a-zA-Z_][a-zA-Z0-9_]* ;
    fragment stringRex: ('"' ([^\\"] | '\\'. )* '"') ;

    intNum: regex(intRex, `Utils.toInt`) ;
    realNum: regex(realRex, `Utils.toReal`) ;
    hexNum: regex(hexRex, `Utils.toHex`) ;
    str: regex(stringRex, `Utils.toEscString`) ;
    identifier: regex(identifierRex, `Utils.toIdString`) ;

    intNode: (intNum | hexNum) `Utils.toIntNode` ;
    realNode: realNum `Utils.toRealNode` ;
    stringNode: str `Utils.toStringNode` ;
    idNode: identifier `Utils.toIdNode` ;

    comma: ',' @ `Annot.Comma` ;
    exprList: `Utils.list` `comma.join(expr.fold(Utils.append))` ;

    vectorNode: '[' exprList `Utils.toVectorNode` ']' ;

    atom: intNode     @ `Annot.Num`
        | realNode    @ `Annot.Num`
        | stringNode  @ `Annot.Str`
        | idNode      @ `Annot.Id`
        | vectorNode
        | '(' expr ')' ;

    qualified: atom ('.' identifier >> `Utils.toQualified`)* ;

    arguments: '(' exprList ')' (app >> `Utils.listApply`)? | app `Utils.toSingleton` ;

    app<SyntaxNode>: qualified (arguments >> `Utils.toApp`)? ; // recursive because of eg "sin cos x"

    ifExpr<SyntaxNode>: 'if' @ `Annot.Kw` `Utils.properties`
                        '(' expr >> `Utils.set("condition")` ')'
                        stmt >> `Utils.set("thenBranch")`
                        (
							'else' @ `Annot.Kw` stmt >> `Utils.set("elseBranch")`
								`Utils.create(IfElse::class.java, true, "condition", "thenBranch", "elseBranch")`
							| `Utils.create(If::class.java, true, "condition", "thenBranch")`
						) ;

	block: '{' stmts `Utils.toBlock` '}' ;

	absExpr: '|' expr `Utils.toUnary(Op.Abs)` '|' ;

    term: app | ifExpr | block | absExpr ;

    literal<SyntaxNode>: '-' literal `Utils.toUnary(Op.Neg)`
                       | '/' literal `Utils.toUnary(Op.Recip)`
                       | literal
                       ;

    cons: literal (':' literal >> `Utils.toBinary(Op.Cons)`)? ;

    pow<SyntaxNode>: cons ('^' pow >> `Utils.toBinary(Op.Pow)`)? ;

    product: pow ('*' pow >> `Utils.toBinary(Op.Mul)` | '/' pow >> `Utils.toBinary(Op.Div)` | '%'  pow >> `Utils.toBinary(Op.Mod)` )* ;

    sum: product ('+' product >> `Utils.toBinary(Op.Add)` | '-' product >> `Utils.toBinary(Op.Sub)` )* ;

	cmp : sum
		( '>' sum >> `Utils.toBinary(Op.Greater)`
		| '>=' sum >> `Utils.toBinary(Op.GreaterEqual)`
		| '<=' sum >> `Utils.toBinary(Op.LessEqual)`
		| '<' sum >> `Utils.toBinary(Op.Less)`
		| '==' sum >> `Utils.toBinary(Op.Equal)`
		| '!=' sum >> `Utils.toBinary(Op.NotEqual)`
		)? ;

	logicalLit: 'not' cmp `Utils.toUnary(Op.Not)` | cmp ;
	logicalAnd: logicalLit ('and' logicalLit >> `Utils.toBinary(Op.And)`)* ;
	logicalXor: logicalAnd ('xor' logicalAnd >> `Utils.toBinary(Op.Xor)`)* ;
	logicalOr: logicalXor ('or' logicalXor >> `Utils.toBinary(Op.Or)`)* ;

	expr<SyntaxNode>: logicalOr ;

	exprstmt: expr ('=' expr >> `Utils.toBinary(Op.Assign)`)? ;

	whilestmt: 'while' @ `Annot.Kw` `Utils.properties`
			   '(' expr >> `Utils.set("condition")` ')'
			   (stmt >> `Utils.set("body")`)?
			   `Utils.create(While::class.java, true, "condition", "body")` ;

	forstmt: 'for' @ `Annot.Kw` `Utils.properties` '(' identifier >> `Utils.set("name")` 'in' expr >> `Utils.set("range")` ')' stmt  >> `Utils.set("body")`
			 `Utils.create(For::class.java, true, "name", "range", "body")`;

 	stmt<SyntaxNode>: (exprstmt | whilestmt | forstmt) ';'? ;

	vardecl: 'var' @ `Annot.DeclKw` `Utils.properties`
	            identifier >> `Utils.set("name")`
				(':' identifier >> `Utils.set("type")`)?
				('=' expr >> `Utils.set("init")`)?
				`Utils.create(VarDecl::class.java, true, "name", "type", "init")` ;

	parameter: 'var' @ `Annot.DeclKw` `Utils.properties` identifier >> `Utils.set("name")`
	            (':' identifier >> `Utils.set("type")`)?
	            `Utils.create(VarParameter::class.java, true, "name", "type")`
	            | idNode ;

	parameters: `Utils.list` `comma.join(parameter.fold(Utils.append))` ;

	fundecl: 'fun' @ `Annot.DeclKw` `Utils.properties` identifier >> `Utils.set("name")`
				'(' parameters >> `Utils.set("parameters")` ')'
				block >> `Utils.set("body")`
				`Utils.create(FunDecl::class.java, true, "name", "parameters", "body")` ;

	classdecl: 'class' @ `Annot.DeclKw` `Utils.properties` identifier >> `Utils.set("name")`
				('(' parameters ')' | `Utils.list`) >> `Utils.set("parameters")`
				block >> `Utils.set("body")`
				`Utils.create(ClassDecl::class.java, true, "name", "parameters", "body")` ;

	defdecl: 'def' @ `Annot.DeclKw`  `Utils.properties` identifier >> `Utils.set("name")`
	         '=' expr >> `Utils.set("body")`
	         `Utils.create(DefDecl::class.java, true, "name", "body")`; // alias.

	decl: (vardecl | fundecl | classdecl | defdecl) ';'? ;

	stmts<`List<SyntaxNode>`>: `Utils.list` ((stmt | decl) @ `Annot.Stmt` >> `Utils.append`)* ;
	program: stmts `Utils.toBlock` ;
}